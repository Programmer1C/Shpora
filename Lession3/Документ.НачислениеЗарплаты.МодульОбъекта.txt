
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОсновныеНачисления.Записывать=Истина;
	Движения.ОсновныеНачисления.Очистить();
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	НАЧАЛОПЕРИОДА(НачислениеЗарплатыОсновныеНачисления.Ссылка.Дата, МЕСЯЦ) КАК ПериодРегистрации,
	             |	НачислениеЗарплатыОсновныеНачисления.Сотрудник,
	             |	НачислениеЗарплатыОсновныеНачисления.Подразделение,
	             |	НачислениеЗарплатыОсновныеНачисления.График,
	             |	НачислениеЗарплатыОсновныеНачисления.ВидРасчета,
	             |	НачислениеЗарплатыОсновныеНачисления.ДатаНачала КАК ПериодДействияНачало,
	             |	НачислениеЗарплатыОсновныеНачисления.ДатаОкончания КАК ПериодДействияКонец,
	             |	НАЧАЛОПЕРИОДА(НачислениеЗарплатыОсновныеНачисления.ДатаНачала, МЕСЯЦ) КАК НачалоРасчетногоПериода
	             |ПОМЕСТИТЬ ТабЧастьОкладВечерниеЧасы
	             |ИЗ
	             |	Документ.НачислениеЗарплаты.ОсновныеНачисления КАК НачислениеЗарплатыОсновныеНачисления
	             |ГДЕ
	             |	НачислениеЗарплатыОсновныеНачисления.Ссылка = &Ссылка
	             |	И (НачислениеЗарплатыОсновныеНачисления.ВидРасчета = &ВидРасчетаОклад
	             |			ИЛИ НачислениеЗарплатыОсновныеНачисления.ВидРасчета = &ВидРасчетаВечерниеЧасы)
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	ТабЧастьОкладВечерниеЧасыСМаксПериодом.ПериодРегистрации,
	             |	ТабЧастьОкладВечерниеЧасыСМаксПериодом.Сотрудник,
	             |	ТабЧастьОкладВечерниеЧасыСМаксПериодом.Подразделение,
	             |	ТабЧастьОкладВечерниеЧасыСМаксПериодом.График,
	             |	ТабЧастьОкладВечерниеЧасыСМаксПериодом.ВидРасчета,
	             |	ТабЧастьОкладВечерниеЧасыСМаксПериодом.ПериодДействияНачало,
	             |	ТабЧастьОкладВечерниеЧасыСМаксПериодом.ПериодДействияКонец,
	             |	СведенияОСотрудниках.Оклад КАК Параметр,
	             |	NULL КАК БазовыйПериодНачало,
	             |	NULL КАК БазовыйПериодКонец
	             |ИЗ
	             |	(ВЫБРАТЬ
	             |		ТабЧастьОкладВечерниеЧасы.ПериодРегистрации КАК ПериодРегистрации,
	             |		ТабЧастьОкладВечерниеЧасы.Сотрудник КАК Сотрудник,
	             |		ТабЧастьОкладВечерниеЧасы.Подразделение КАК Подразделение,
	             |		ТабЧастьОкладВечерниеЧасы.График КАК График,
	             |		ТабЧастьОкладВечерниеЧасы.ВидРасчета КАК ВидРасчета,
	             |		ТабЧастьОкладВечерниеЧасы.ПериодДействияНачало КАК ПериодДействияНачало,
	             |		ТабЧастьОкладВечерниеЧасы.ПериодДействияКонец КАК ПериодДействияКонец,
	             |		ТабЧастьОкладВечерниеЧасы.НачалоРасчетногоПериода КАК НачалоРасчетногоПериода,
	             |		МАКСИМУМ(СведенияОСотрудниках.Период) КАК МаксимальныйПериод
	             |	ИЗ
	             |		ТабЧастьОкладВечерниеЧасы КАК ТабЧастьОкладВечерниеЧасы
	             |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках КАК СведенияОСотрудниках
	             |			ПО ТабЧастьОкладВечерниеЧасы.Сотрудник = СведенияОСотрудниках.Сотрудник
	             |				И ТабЧастьОкладВечерниеЧасы.Подразделение = СведенияОСотрудниках.Подразделение
	             |				И ТабЧастьОкладВечерниеЧасы.НачалоРасчетногоПериода >= СведенияОСотрудниках.Период
	             |	
	             |	СГРУППИРОВАТЬ ПО
	             |		ТабЧастьОкладВечерниеЧасы.ПериодДействияКонец,
	             |		ТабЧастьОкладВечерниеЧасы.ПериодДействияНачало,
	             |		ТабЧастьОкладВечерниеЧасы.НачалоРасчетногоПериода,
	             |		ТабЧастьОкладВечерниеЧасы.Подразделение,
	             |		ТабЧастьОкладВечерниеЧасы.График,
	             |		ТабЧастьОкладВечерниеЧасы.ВидРасчета,
	             |		ТабЧастьОкладВечерниеЧасы.ПериодРегистрации,
	             |		ТабЧастьОкладВечерниеЧасы.Сотрудник) КАК ТабЧастьОкладВечерниеЧасыСМаксПериодом
	             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках КАК СведенияОСотрудниках
	             |		ПО ТабЧастьОкладВечерниеЧасыСМаксПериодом.Сотрудник = СведенияОСотрудниках.Сотрудник
	             |			И ТабЧастьОкладВечерниеЧасыСМаксПериодом.Подразделение = СведенияОСотрудниках.Подразделение
	             |			И ТабЧастьОкладВечерниеЧасыСМаксПериодом.МаксимальныйПериод = СведенияОСотрудниках.Период
	             |
	             |ОБЪЕДИНИТЬ ВСЕ
	             |
	             |ВЫБРАТЬ
	             |	НАЧАЛОПЕРИОДА(НачислениеЗарплатыОсновныеНачисления.Ссылка.Дата, МЕСЯЦ),
	             |	НачислениеЗарплатыОсновныеНачисления.Сотрудник,
	             |	НачислениеЗарплатыОсновныеНачисления.Подразделение,
	             |	ЗНАЧЕНИЕ(Справочник.Графики.Пятидневка),
	             |	НачислениеЗарплатыОсновныеНачисления.ВидРасчета,
	             |	НачислениеЗарплатыОсновныеНачисления.ДатаНачала,
	             |	НачислениеЗарплатыОсновныеНачисления.ДатаОкончания,
	             |	NULL,
	             |	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(НачислениеЗарплатыОсновныеНачисления.ДатаНачала, МЕСЯЦ, -2), МЕСЯЦ),
	             |	КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(НачислениеЗарплатыОсновныеНачисления.ДатаНачала, МЕСЯЦ, -1), МЕСЯЦ)
	             |ИЗ
	             |	Документ.НачислениеЗарплаты.ОсновныеНачисления КАК НачислениеЗарплатыОсновныеНачисления
	             |ГДЕ
	             |	НачислениеЗарплатыОсновныеНачисления.Ссылка = &Ссылка
	             |	И НачислениеЗарплатыОсновныеНачисления.ВидРасчета = &ВидРасчетаКомандировка";
				 
				 Запрос.УстановитьПараметр("Ссылка", ссылка);
				 Запрос.УстановитьПараметр("ВидРасчетаОклад", ПланыВидовРасчета.ОсновныеНачисления.Оклад);
				 Запрос.УстановитьПараметр("ВидРасчетаКомандировка", ПланыВидовРасчета.ОсновныеНачисления.Командировка);
				 Запрос.УстановитьПараметр("ВидРасчетаВечерниеЧасы", ПланыВидовРасчета.ОсновныеНачисления.ВечерниеЧасы);
				 
				 Выборка=Запрос.Выполнить().Выбрать();
				 Пока Выборка.Следующий() Цикл
					 Движение=Движения.ОсновныеНачисления.Добавить();
					 ЗаполнитьЗначенияСвойств(Движение,Выборка);
				 КонецЦикла;
				 
				 СторноЗаписи=Движения.ОсновныеНачисления.ПолучитьДополнение();
				 Для Каждого Запись из СторноЗаписи Цикл
					 Движение=Движения.ОсновныеНачисления.Добавить();
					 ЗаполнитьЗначенияСвойств(Движение, Запись);
					 Движение.ПериодДействияНачало=Запись.ПериодДействияНачалоСторно;
					 Движение.ПериодДействияКонец=Запись.ПериодДействияКонецСторно;
					 Движение.ПериодРегистрации=Запись.ПериодРегистрацииСторно;
					 Движение.Сторно=Истина;
				 КонецЦикла;
				 
				 Движения.Записать();
				 
				 Расчет.РассчитатьНачисления(Ссылка);
				 
	
КонецПроцедуры
