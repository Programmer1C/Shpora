
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОсновныеНачисления.Записывать=Истина;
	Движения.ОсновныеНачисления.Очистить();	
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	НачислениеЗарплатыОсновныеНачисления.Сотрудник,
	             |	НачислениеЗарплатыОсновныеНачисления.Подразделение,
	             |	НачислениеЗарплатыОсновныеНачисления.ДатаНачала КАК ПериодДействияНачало,
	             |	НачислениеЗарплатыОсновныеНачисления.ДатаОкончания КАК ПериодДействияКонец,
	             |	НАЧАЛОПЕРИОДА(НачислениеЗарплатыОсновныеНачисления.Ссылка.Дата, МЕСЯЦ) КАК ПериодРегистрации,
	             |	НачислениеЗарплатыОсновныеНачисления.ВидРасчета,
	             |	НачислениеЗарплатыОсновныеНачисления.НомерСтроки
	             |ПОМЕСТИТЬ ТабЧастьОклад
	             |ИЗ
	             |	Документ.НачислениеЗарплаты.ОсновныеНачисления КАК НачислениеЗарплатыОсновныеНачисления
	             |ГДЕ
	             |	НачислениеЗарплатыОсновныеНачисления.Ссылка = &Ссылка
	             |	И НачислениеЗарплатыОсновныеНачисления.ВидРасчета = &ВидРасчетаОклад
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	ТабЧастьОклад.Сотрудник,
	             |	ТабЧастьОклад.Подразделение,
	             |	СведенияОСотрудниках.Оклад КАК Параметр,
	             |	СведенияОСотрудниках.Период КАК ПериодДействияНачало,
	             |	ТабЧастьОклад.ПериодДействияКонец,
	             |	ТабЧастьОклад.ПериодРегистрации,
	             |	ТабЧастьОклад.ВидРасчета,
	             |	ТабЧастьОклад.НомерСтроки КАК НомерСтроки
	             |ИЗ
	             |	ТабЧастьОклад КАК ТабЧастьОклад
	             |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках КАК СведенияОСотрудниках
	             |		ПО ТабЧастьОклад.Сотрудник = СведенияОСотрудниках.Сотрудник
	             |			И ТабЧастьОклад.Подразделение = СведенияОСотрудниках.Подразделение
	             |			И ТабЧастьОклад.ПериодДействияНачало < СведенияОСотрудниках.Период
	             |			И ТабЧастьОклад.ПериодДействияКонец >= СведенияОСотрудниках.Период
	             |
	             |ОБЪЕДИНИТЬ ВСЕ
	             |
	             |ВЫБРАТЬ
	             |	ТабЧастьОкладСМаксПериодом.Сотрудник,
	             |	ТабЧастьОкладСМаксПериодом.Подразделение,
	             |	СведенияОСотрудниках.Оклад,
	             |	ТабЧастьОкладСМаксПериодом.ПериодДействияНачало,
	             |	ТабЧастьОкладСМаксПериодом.ПериодДействияКонец,
	             |	ТабЧастьОкладСМаксПериодом.ПериодРегистрации,
	             |	ТабЧастьОкладСМаксПериодом.ВидРасчета,
	             |	ТабЧастьОкладСМаксПериодом.НомерСтроки
	             |ИЗ
	             |	(ВЫБРАТЬ
	             |		ТабЧастьОклад.Сотрудник КАК Сотрудник,
	             |		ТабЧастьОклад.Подразделение КАК Подразделение,
	             |		ТабЧастьОклад.ПериодДействияНачало КАК ПериодДействияНачало,
	             |		ТабЧастьОклад.ПериодДействияКонец КАК ПериодДействияКонец,
	             |		ТабЧастьОклад.ПериодРегистрации КАК ПериодРегистрации,
	             |		МАКСИМУМ(СведенияОСотрудниках.Период) КАК МаксимальныйПериод,
	             |		ТабЧастьОклад.ВидРасчета КАК ВидРасчета,
	             |		ТабЧастьОклад.НомерСтроки КАК НомерСтроки
	             |	ИЗ
	             |		ТабЧастьОклад КАК ТабЧастьОклад
	             |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках КАК СведенияОСотрудниках
	             |			ПО ТабЧастьОклад.Сотрудник = СведенияОСотрудниках.Сотрудник
	             |				И ТабЧастьОклад.Подразделение = СведенияОСотрудниках.Подразделение
	             |				И ТабЧастьОклад.ПериодДействияНачало >= СведенияОСотрудниках.Период
	             |	
	             |	СГРУППИРОВАТЬ ПО
	             |		ТабЧастьОклад.ПериодДействияКонец,
	             |		ТабЧастьОклад.ПериодРегистрации,
	             |		ТабЧастьОклад.Сотрудник,
	             |		ТабЧастьОклад.Подразделение,
	             |		ТабЧастьОклад.ПериодДействияНачало,
	             |		ТабЧастьОклад.ВидРасчета,
	             |		ТабЧастьОклад.НомерСтроки) КАК ТабЧастьОкладСМаксПериодом
	             |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках КАК СведенияОСотрудниках
	             |		ПО ТабЧастьОкладСМаксПериодом.Сотрудник = СведенияОСотрудниках.Сотрудник
	             |			И ТабЧастьОкладСМаксПериодом.Подразделение = СведенияОСотрудниках.Подразделение
	             |			И ТабЧастьОкладСМаксПериодом.МаксимальныйПериод = СведенияОСотрудниках.Период
	             |
	             |УПОРЯДОЧИТЬ ПО
	             |	ПериодДействияНачало
	             |ИТОГИ ПО
	             |	НомерСтроки
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	НачислениеЗарплатыОсновныеНачисления.Сотрудник,
	             |	НачислениеЗарплатыОсновныеНачисления.Подразделение,
	             |	НАЧАЛОПЕРИОДА(НачислениеЗарплатыОсновныеНачисления.Ссылка.Дата, МЕСЯЦ) КАК ПериодРегистрации,
	             |	ВЫБОР
	             |		КОГДА НачислениеЗарплатыОсновныеНачисления.ВидРасчета = &ВидРасчетаПремия
	             |			ТОГДА НачислениеЗарплатыОсновныеНачисления.Параметр
	             |		ИНАЧЕ 0
	             |	КОНЕЦ КАК Параметр,
	             |	НачислениеЗарплатыОсновныеНачисления.ВидРасчета,
	             |	НачислениеЗарплатыОсновныеНачисления.ДатаНачала КАК ПериодДействияНачало,
	             |	НачислениеЗарплатыОсновныеНачисления.ДатаОкончания КАК ПериодДействияКонец,
	             |	ВЫБОР
	             |		КОГДА НачислениеЗарплатыОсновныеНачисления.ВидРасчета = &ВидРасчетаПроизвольнаяСумма
	             |			ТОГДА НачислениеЗарплатыОсновныеНачисления.Параметр
	             |		ИНАЧЕ 0
	             |	КОНЕЦ КАК Результат
	             |ИЗ
	             |	Документ.НачислениеЗарплаты.ОсновныеНачисления КАК НачислениеЗарплатыОсновныеНачисления
	             |ГДЕ
	             |	НачислениеЗарплатыОсновныеНачисления.Ссылка = &Ссылка
	             |	И НачислениеЗарплатыОсновныеНачисления.ВидРасчета <> &ВидРасчетаОклад";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидРасчетаОклад", ПланыВидовРасчета.ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("ВидРасчетаПремия", ПланыВидовРасчета.ОсновныеНачисления.Премия);	
	Запрос.УстановитьПараметр("ВидРасчетаПроизвольнаяСумма", ПланыВидовРасчета.ОсновныеНачисления.ПроизвольнаяСумма);	
	
	Результаты = Запрос.ВыполнитьПакет();                                                      	
	
	ВыборкаИтоги = Результаты[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Движение=Неопределено;
	Пока ВыборкаИтоги.Следующий() Цикл
		ЭтоСледующаяЗапись=Ложь;	
		Выборка=ВыборкаИтоги.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если ЭтоСледующаяЗапись Тогда
				//Переопределяем окончание периода действия для прошлой записи набора
				Движение.ПериодДействияКонец=Выборка.ПериодДействияНачало-1;
			КонецЕсли;
			
			Движение=Движения.ОсновныеНачисления.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
			ЭтоСледующаяЗапись=Истина;
		КонецЦикла;
	КонецЦикла;
	
	Выборка = Результаты[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение=Движения.ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
	Движения.Записать();
	
	Расчет.РассчитатьНачисления(Ссылка);	
	
	// регистр РасчетыССотрудниками
	Движения.РасчетыССотрудниками.Записывать=Истина;
	Движения.РасчетыССотрудниками.Очистить();
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	ОсновныеНачисления.Сотрудник,
	             |	ОсновныеНачисления.ВидРасчета,
	             |	СУММА(ОсновныеНачисления.Результат) КАК Сумма,
	             |	ОсновныеНачисления.Подразделение
	             |ИЗ
	             |	РегистрРасчета.ОсновныеНачисления КАК ОсновныеНачисления
	             |ГДЕ
	             |	ОсновныеНачисления.Регистратор = &Регистратор
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	ОсновныеНачисления.Сотрудник,
	             |	ОсновныеНачисления.ВидРасчета,
	             |	ОсновныеНачисления.Подразделение";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение=Движения.РасчетыССотрудниками.Добавить();
		Движение.Период=Дата;
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;        	
	
	
КонецПроцедуры
