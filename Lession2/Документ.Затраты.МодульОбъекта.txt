
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Управленческий.Записывать=Истина;
	Движения.Управленческий.Очистить();
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	ЗатратыСписокПроектов.Проект КАК Проект,
	|	СУММА(ЗатратыСписокПроектов.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ТабЧасть
	|ИЗ
	|	Документ.Затраты.СписокПроектов КАК ЗатратыСписокПроектов
	|ГДЕ
	|	ЗатратыСписокПроектов.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗатратыСписокПроектов.Проект
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Проект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабЧасть.Проект КАК Проект,
	|	ТабЧасть.Сумма КАК Сумма,
	|	ЕСТЬNULL(УправленческийОборотыДтКт.КоличествоОборотКт, 0) КАК Количество,
	|	УправленческийОборотыДтКт.СубконтоКт1 КАК Номенклатура
	|ИЗ
	|	ТабЧасть КАК ТабЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.ОборотыДтКт(
	|				&НачалоДня,
	|				&КонецДня,
	|				,
	|				СчетДт = &СчетПрибылиИУбытки,
	|				&СубконтоПрибыли,
	|				СчетКт = &СчетТовары,
	|				&СубконтоТовары,
	|				СубконтоДт1 В
	|					(ВЫБРАТЬ
	|						Т.Проект
	|					ИЗ
	|						ТабЧасть КАК Т)) КАК УправленческийОборотыДтКт
	|		ПО ТабЧасть.Проект = УправленческийОборотыДтКт.СубконтоДт1
	|ИТОГИ
	|	МАКСИМУМ(Сумма),
	|	СУММА(Количество)
	|ПО
	|	Проект";
	
	СубконтоПрибыли=Новый Массив(1);
	СубконтоПрибыли[0]=ПланыВидовХарактеристик.ВидыСубконто.Проект;
	СубконтоТовары=Новый Массив(1);
	СубконтоТовары[0]=ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня(Дата));
	Запрос.УстановитьПараметр("КонецДня", КонецДня(Дата));
	Запрос.УстановитьПараметр("СчетПрибылиИУбытки", ПланыСчетов.Управленческий.ПрибылиУбытки);
	Запрос.УстановитьПараметр("СубконтоПрибыли", СубконтоПрибыли);
	Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
	Запрос.УстановитьПараметр("СубконтоТовары", СубконтоТовары);
	
	
	ВыборкаПроект = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПроект.Следующий() Цикл
		КоличествоОсталосьСписать = ВыборкаПроект.Количество;
		СуммаОсталосьСписать = ВыборкаПроект.Сумма;
		
		КоличествоПоПроекту=ВыборкаПроект.Количество;
		СуммаПоПроекту=ВыборкаПроект.Сумма;
		
		Выборка = ВыборкаПроект.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Проводка = Движения.Управленческий.Добавить();
			Проводка.Период=Дата;
			Проводка.СчетДт=ПланыСчетов.Управленческий.ПрибылиУбытки;
			Проводка.СчетКт=ПланыСчетов.Управленческий.ОбщехозяйственныеЗатраты;
			Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура]=Выборка.Номенклатура;
			Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Проект]=Выборка.Проект;
			
			Если КоличествоОсталосьСписать=Выборка.Количество Тогда
				Списываем=СуммаОсталосьСписать;
			Иначе
				Списываем=Окр(Выборка.Количество / КоличествоПоПроекту * СуммаПоПроекту,2);
			КонецЕсли;
			
			Проводка.Сумма=Списываем;
			
			КоличествоОсталосьСписать=КоличествоОсталосьСписать-Выборка.Количество;
			СуммаОсталосьСписать=СуммаОсталосьСписать-Списываем;
		КонецЦикла;
	КонецЦикла;
	
	
	
	
КонецПроцедуры
