
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	
	//Оперативный учет
	Движения.ОстаткиТоваров.Записывать=Истина;
	Движения.ОстаткиТоваров.БлокироватьДляИзменения=Истина;
	Движения.ОстаткиТоваров.Очистить();
	Движения.ОстаткиТоваров.Записать();
	
	Движения.Продажи.Записывать=Истина;
	Движения.Продажи.Очистить();
	
	Блокировка=Новый БлокировкаДанных;
	ЭлементБлокировки=Блокировка.Добавить("РегистрНакопления.ОстаткиТоваров");
	ЭлементБлокировки.ИсточникДанных=СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Партия", "Партия");
	Блокировка.Заблокировать();
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	             |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	             |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма,
	             |	РасходнаяНакладнаяСписокНоменклатуры.Партия КАК Партия
	             |ПОМЕСТИТЬ ТабЧасть
	             |ИЗ
	             |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	             |ГДЕ
	             |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
	             |	РасходнаяНакладнаяСписокНоменклатуры.Партия
	             |
	             |ИНДЕКСИРОВАТЬ ПО
	             |	Номенклатура,
	             |	Партия
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	ТабЧасть.Номенклатура КАК Номенклатура,
	             |	ТабЧасть.Номенклатура.Представление,
	             |	ТабЧасть.Количество КАК Количество,
	             |	ТабЧасть.Сумма КАК Сумма,
	             |	ЕСТЬNULL(ОстаткиТоваровОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	             |	ТабЧасть.Номенклатура.Услуга КАК Услуга,
	             |	ЕСТЬNULL(ОстаткиТоваровОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
	             |	ТабЧасть.Партия.Представление КАК ПартияПредставление,
	             |	ТабЧасть.Партия КАК Партия
	             |ИЗ
	             |	ТабЧасть КАК ТабЧасть
	             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваров.Остатки(
	             |				&МоментИтогов,
	             |				(Номенклатура, Партия) В
	             |					(ВЫБРАТЬ
	             |						Т.Номенклатура,
	             |						Т.Партия
	             |					ИЗ
	             |						ТабЧасть КАК Т)) КАК ОстаткиТоваровОстатки
	             |		ПО ТабЧасть.Номенклатура = ОстаткиТоваровОстатки.Номенклатура
	             |			И ТабЧасть.Партия = ОстаткиТоваровОстатки.Партия
	             |ИТОГИ
	             |	СУММА(Количество),
	             |	СУММА(Сумма)
	             |ПО
	             |	Номенклатура";
	
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментИтогов", МоментВремени());
	
	ВыборкаНоменклатура=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		СуммаСписанияНоменклатуры=0;		
		Если Не ВыборкаНоменклатура.Услуга Тогда
			Выборка = ВыборкаНоменклатура.Выбрать();
			Пока Выборка.Следующий() Цикл 
				Превышение = Выборка.Количество - Выборка.КоличествоОстаток;
				Если Превышение>0 Тогда
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = "Превышение остатка по номенклатуре "+Выборка.НоменклатураПредставление+"и партии "+ Выборка.ПартияПредставление+" в количестве "+Превышение;
					Сообщение.Сообщить();
					Отказ=Истина;
				КонецЕсли;
				
				Если Отказ Тогда
					Продолжить;
				КонецЕсли;				
				
				Движение=Движения.ОстаткиТоваров.ДобавитьРасход();
				Движение.Период=Дата;
				Движение.Партия=Выборка.Партия;
				Движение.Номенклатура=Выборка.Номенклатура;
				Движение.Количество = Выборка.Количество;
				
				Если Выборка.Количество=Выборка.КоличествоОстаток Тогда
					СуммаСписания=Выборка.СуммаОстаток;
				Иначе
					СуммаСписания=Выборка.Количество/Выборка.КоличествоОстаток * Выборка.СуммаОстаток;
				КонецЕсли;
				
				Движение.Сумма=СуммаСписания;
				СуммаСписанияНоменклатуры=СуммаСписанияНоменклатуры+СуммаСписания;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Движение=Движения.Продажи.Добавить();
		Движение.Период=Дата;
		Движение.Номенклатура=ВыборкаНоменклатура.Номенклатура;
		Движение.Количество=ВыборкаНоменклатура.Количество;
		Движение.Выручка=ВыборкаНоменклатура.Сумма;
		Движение.Себестоимость=СуммаСписанияНоменклатуры;
		
	КонецЦикла;
	
	
	//Бухгалтерский учет
	Движения.Управленческий.Записывать=Истина;
	Движения.Управленческий.БлокироватьДляИзменения=Истина;
	Движения.Управленческий.Очистить();
	Движения.Управленческий.Записать();
	
	Блокировка=Новый БлокировкаДанных;
	ЭлементБлокировки=Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
	ЭлементБлокировки.ИсточникДанных=СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
	Блокировка.Заблокировать();
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	             |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	             |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
	             |ПОМЕСТИТЬ ТабЧасть
	             |ИЗ
	             |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	             |ГДЕ
	             |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
	             |
	             |ИНДЕКСИРОВАТЬ ПО
	             |	Номенклатура
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	ТабЧасть.Номенклатура,
	             |	ТабЧасть.Количество,
	             |	ТабЧасть.Сумма,
	             |	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	             |	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
	             |	ТабЧасть.Номенклатура.Представление,
	             |	ТабЧасть.Номенклатура.Услуга КАК Услуга
	             |ИЗ
	             |	ТабЧасть КАК ТабЧасть
	             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
	             |				&МоментИтогов,
	             |				Счет = &СчетТовары,
	             |				&Субконто,
	             |				Субконто1 В
	             |					(ВЫБРАТЬ
	             |						Т.Номенклатура
	             |					ИЗ
	             |						ТабЧасть КАК Т)) КАК УправленческийОстатки
	             |		ПО ТабЧасть.Номенклатура = УправленческийОстатки.Субконто1";
	
	Субконто = Новый Массив(1);
	Субконто[0]=ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментИтогов", ?(РежимПроведения=РежимПроведенияДокумента.Оперативный, Неопределено, МоментВремени()));
	Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
	Запрос.УстановитьПараметр("Субконто", Субконто);
	
	Выборка=Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Не Выборка.Услуга Тогда
			Превышение=Выборка.Количество - Выборка.КОличествоОстаток;
			Если Превышение>0 Тогда
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Превышение остатка по номенклатуре "+Выборка.НоменклатураПредставление+ " в количестве "+Превышение;
				Сообщение.Сообщить();
				Отказ=Истина;
			КонецЕсли;
			
			Если Отказ Тогда
				Продолжить;
			КонецЕсли;			
			
			Проводка = Движения.Управленческий.Добавить();
			Проводка.Период=Дата;
			Проводка.СчетДт=ПланыСчетов.Управленческий.ПрибылиУбытки;
			Проводка.СчетКт=ПланыСчетов.Управленческий.Товары;
			Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура]=Выборка.Номенклатура;
			Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура]=Выборка.Номенклатура;
			Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Проект]=Проект;
			Проводка.КоличествоКт=Выборка.Количество;
			
			Если Выборка.Количество=Выборка.КоличествоОстаток Тогда
				Проводка.Сумма=Выборка.СуммаОстаток;
			Иначе
				Проводка.Сумма=Выборка.Количество/Выборка.КоличествоОстаток * Выборка.СуммаОстаток;
			КонецЕсли;
			
		КонецЕсли;
			
			Проводка = Движения.Управленческий.Добавить();
			Проводка.Период=Дата;
			Проводка.СчетДт=ПланыСчетов.Управленческий.Покупатели;
			Проводка.СчетКт=ПланыСчетов.Управленческий.ПрибылиУбытки;
			Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура]=Выборка.Номенклатура;
			Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Проект]=Проект;
			
			Проводка.Сумма=Выборка.Сумма;			
		КонецЦикла;
	
	
	
КонецПроцедуры
