
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОсновныеНачисления.Записывать=Истина;
	Движения.ОсновныеНачисления.Очистить();
	
	Движения.ДопНачисления.Записывать=Истина;
	Движения.ДопНачисления.Очистить();
	
	ПериодРегистрации=НачалоМесяца(Дата);
	Для Каждого Строка Из ОсновныеНачисления Цикл
		Движение=Движения.ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Строка);
		Движение.ПериодРегистрации=ПериодРегистрации;
		Движение.ПериодДействияНачало=Строка.ДатаНачала;
		Если Строка.ВидРасчета=ПланыВидовРасчета.ОсновныеНачисления.Оклад Тогда
			Движение.ПериодДействияКонец=КонецДня(Строка.ДатаОкончания);
		Иначе
			Движение.ПериодДействияКонец=Строка.ДатаОкончания;
		КонецЕсли;
	КонецЦикла;
	
	
	Если ДопНачисления.Количество()<>0 Тогда
		//Определяем минимальную и максимальную дату начислений для последующего отбора по периоду регистра бухглатерии
		Запрос = Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	МИНИМУМ(НачислениеЗарплатыДопНачисления.МесяцНачисления) КАК МинДата,
		|	МАКСИМУМ(НачислениеЗарплатыДопНачисления.МесяцНачисления) КАК МаксДата
		|ИЗ
		|	Документ.НачислениеЗарплаты.ДопНачисления КАК НачислениеЗарплатыДопНачисления
		|ГДЕ
		|	НачислениеЗарплатыДопНачисления.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Результат = ЗАпрос.Выполнить();
		
		Данные=Результат.Выбрать();
		Данные.Следующий();
		МинДата=Данные.МинДата;
		МаксДата=Данные.МаксДата;
		
		Запрос.Текст="ВЫБРАТЬ
		|	НачислениеЗарплатыДопНачисления.Сотрудник КАК Сотрудник,
		|	НачислениеЗарплатыДопНачисления.Подразделение КАК Подразделение,
		|	НачислениеЗарплатыДопНачисления.ВидРасчета,
		|	ДОБАВИТЬКДАТЕ(НачислениеЗарплатыДопНачисления.МесяцНачисления, МЕСЯЦ, -1) КАК МесяцБазы,
		|	НАЧАЛОПЕРИОДА(НачислениеЗарплатыДопНачисления.Ссылка.Дата, МЕСЯЦ) КАК ПериодРегистрации,
		|	НачислениеЗарплатыДопНачисления.ПроцентНадбавки
		|ПОМЕСТИТЬ ТабЧастьНадбавка
		|ИЗ
		|	Документ.НачислениеЗарплаты.ДопНачисления КАК НачислениеЗарплатыДопНачисления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник,
		|	Подразделение,
		|	МесяцБазы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТабЧастьНадбавка.Сотрудник,
		|	ТабЧастьНадбавка.Подразделение,
		|	ТабЧастьНадбавка.ВидРасчета,
		|	ТабЧастьНадбавка.ПериодРегистрации,
		|	ЕСТЬNULL(УправленческийОборотыДтКт.СуммаОборот, 0) КАК СуммаПродаж,
		|	ТабЧастьНадбавка.ПроцентНадбавки
		|ИЗ
		|	ТабЧастьНадбавка КАК ТабЧастьНадбавка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.ОборотыДтКт(
		|				&НачалоПериода,
		|				&КонецПериода,
		|				Месяц,
		|				СчетДт = &СчетПокупатели,
		|				,
		|				СчетКт = &СчетПрибыли,
		|				&Субконто,
		|				СубконтоКт1 В
		|					(ВЫБРАТЬ
		|						А.Подразделение
		|					ИЗ
		|						ТабЧастьНадбавка КАК А)) КАК УправленческийОборотыДтКт
		|		ПО ТабЧастьНадбавка.Подразделение = УправленческийОборотыДтКт.СубконтоКт1
		|			И ТабЧастьНадбавка.МесяцБазы = УправленческийОборотыДтКт.Период";
		
		Субконто=Новый Массив(1);
		Субконто[0]=ПланыВидовХарактеристик.ВидыСубконто.Подразделение;
		
		Запрос.УстановитьПараметр("НачалоПериода", ДобавитьМесяц(МинДата, -1));
		Запрос.УстановитьПараметр("КонецПериода", МаксДата-1);
		Запрос.УстановитьПараметр("СчетПокупатели", ПланыСчетов.Управленческий.Покупатели);
		Запрос.УстановитьПараметр("СчетПрибыли", ПланыСчетов.Управленческий.ПрибылиУбытки);
		Запрос.УстановитьПараметр("Субконто", Субконто);
		
		Выборка=Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Движение=Движения.ДопНачисления.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
		КонецЦикла;
		
	КонецЕсли;
	
	Движения.Записать();
	
	Расчет.РассчитатьНачисления (Ссылка);
	
КонецПроцедуры
