
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Взаиморасчеты.Записывать=Истина;
	Движения.Взаиморасчеты.Очистить();
	Движения.Взаиморасчеты.Записать();
	
	Блокировка=Новый БлокировкаДанных;
	ЭлементБлокировки=Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);	
	Блокировка.Заблокировать();     
	
	ПроектАванс= Справочники.Проекты.Аванс;
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	ВзаиморасчетыОстатки.Проект,
	             |	ВзаиморасчетыОстатки.СуммаОстаток
	             |ИЗ
	             |	РегистрНакопления.Взаиморасчеты.Остатки(
	             |			&МоментВремени,
	             |			Контрагент = &Контрагент
	             |				И Проект <> &Аванс) КАК ВзаиморасчетыОстатки
	             |
	             |УПОРЯДОЧИТЬ ПО
	             |	ВзаиморасчетыОстатки.Проект.ДатаОплаты";
				 
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Аванс", ПроектАванс);
	
	Выборка=Запрос.Выполнить().Выбрать();
	ОсталосьСписать=Сумма;
	Пока Выборка.Следующий() и ОсталосьСписать<>0 Цикл
		Списываем = Мин (ОсталосьСписать, Выборка.СуммаОстаток);
		Движение=Движения.Взаиморасчеты.ДобавитьРасход();
		Движение.Период=Дата;
		Движение.Контрагент=Контрагент;
		Движение.Проект=Выборка.Проект;
		Движение.Сумма=Списываем;
		ОсталосьСписать=ОсталосьСписать-Списываем;
	КонецЦикла;
	
	Если ОсталосьСписать<>0 Тогда
		Движение=Движения.Взаиморасчеты.ДобавитьРасход();
		Движение.Период=Дата;
		Движение.Контрагент=Контрагент;
		Движение.Проект=ПроектАванс;
		Движение.Сумма=ОсталосьСписать;
	КонецЕсли;

	
КонецПроцедуры
