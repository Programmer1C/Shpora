
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//Оперативный учет
	Если ОУ Тогда
		Движения.Взаиморасчеты.Записывать=Истина;
		Движения.Взаиморасчеты.Очистить();
		Движения.Взаиморасчеты.Записать();
		
		ПроектАванс=Справочники.Проекты.Аванс;
		
		Блокировка=Новый БлокировкаДанных;
		ЭлементБлокировки=Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
		ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
		ЭлементБлокировки.УстановитьЗначение("Проект", ПроектАванс);
		
		ЭлементБлокировки=Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
		ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
		ЭлементБлокировки.УстановитьЗначение("Проект", Проект);
		Блокировка.Заблокировать();
		
		
		
		Движение=Движения.Взаиморасчеты.ДобавитьПриход();
		Движение.Период=Дата;
		Движение.Контрагент=Контрагент;
		Движение.Проект=Проект;
		Движение.Сумма=СуммаПоДокументу;
		
		
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	-ВзаиморасчетыОстатки.СуммаОстаток КАК СуммаАванс,
		|	ВзаиморасчетыОстатки.Контрагент
		|ИЗ
		|	РегистрНакопления.Взаиморасчеты.Остатки(
		|			&МоментИтогов,
		|			Контрагент = &Контрагент
		|				И Проект = &Аванс) КАК ВзаиморасчетыОстатки";
		
		Запрос.УстановитьПараметр("МоментИтогов", МоментВремени());
		Запрос.УстановитьПараметр("Контрагент", Контрагент);
		Запрос.УстановитьПараметр("Аванс", ПроектАванс);
		
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			Данные=Результат.Выбрать();
			Данные.Следующий();
			СуммаАванса=Данные.СуммаАванс;
			Гасим=Мин (СуммаАванса, СуммаПоДокументу);
			
			Движение=Движения.Взаиморасчеты.ДобавитьПриход();
			Движение.Период=Дата;
			Движение.Контрагент=Контрагент;
			Движение.Проект=ПроектАванс;
			Движение.Сумма=Гасим;
			
			Движение=Движения.Взаиморасчеты.ДобавитьРасход();
			Движение.Период=Дата;
			Движение.Контрагент=Контрагент;
			Движение.Проект=Проект;
			Движение.Сумма=Гасим;
		КонецЕсли;
		
	КонецЕсли;
	
	
	//Бухгалтерский учет
	Если БУ Тогда 
		Движения.Управленческий.Записывать=Истина;
		Движения.Управленческий.БлокироватьДляИзменения=Истина;
		Движения.Управленческий.Очистить();
		Движения.Управленческий.Записать();
		
		Блокировка=Новый БлокировкаДанных;
		ЭлементБлокировки=Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
		ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
		ЭлементБлокировки.ИсточникДанных=СписокНоменклатуры;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.ИнвНомер, "ИнвНомер");
		Блокировка.Заблокировать();
		
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		             |	РасходнаяНакладнаяСписокНоменклатуры.ИнвНомер КАК ИнвНомер,
		             |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		             |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		             |ПОМЕСТИТЬ ТабЧасть
		             |ИЗ
		             |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		             |ГДЕ
		             |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		             |
		             |СГРУППИРОВАТЬ ПО
		             |	РасходнаяНакладнаяСписокНоменклатуры.ИнвНомер,
		             |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		             |
		             |ИНДЕКСИРОВАТЬ ПО
		             |	Номенклатура,
		             |	ИнвНомер
		             |;
		             |
		             |////////////////////////////////////////////////////////////////////////////////
		             |ВЫБРАТЬ
		             |	ТабЧасть.Номенклатура КАК Номенклатура,
		             |	ТабЧасть.ИнвНомер,
		             |	ТабЧасть.Количество КАК Количество,
		             |	ТабЧасть.Сумма,
		             |	ЕСТЬNULL(УправленческийОстаткиПоНоменклатуре.СуммаОстаток, 0) КАК СуммаОстаток,
		             |	ЕСТЬNULL(УправленческийОстаткиПоНоменклатуреИИнвНомеру.КоличествоОстаток, 0) КАК КоличествоОстаток,
		             |	ТабЧасть.Номенклатура.Представление,
		             |	ТабЧасть.ИнвНомер.Представление,
		             |	ЕСТЬNULL(УправленческийОстаткиПоНоменклатуре.КоличествоОстаток, 0) КАК КоличествоОстатокПоНоменклатуре
		             |ИЗ
		             |	ТабЧасть КАК ТабЧасть
		             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
		             |				&МоментИтогов,
		             |				Счет = &СчетТовары,
		             |				&СубконтоНоменклатура,
		             |				Субконто1 В
		             |					(ВЫБРАТЬ
		             |						А.Номенклатура
		             |					ИЗ
		             |						ТабЧасть КАК А)) КАК УправленческийОстаткиПоНоменклатуре
		             |		ПО ТабЧасть.Номенклатура = УправленческийОстаткиПоНоменклатуре.Субконто1
		             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
		             |				&МоментИтогов,
		             |				Счет = &СчетТовары,
		             |				&СубконтоНомИИнвНомер,
		             |				(Субконто1, Субконто2) В
		             |					(ВЫБРАТЬ
		             |						А.Номенклатура,
		             |						А.ИнвНомер
		             |					ИЗ
		             |						ТабЧасть КАК А)) КАК УправленческийОстаткиПоНоменклатуреИИнвНомеру
		             |		ПО ТабЧасть.Номенклатура = УправленческийОстаткиПоНоменклатуреИИнвНомеру.Субконто1
		             |			И ТабЧасть.ИнвНомер = УправленческийОстаткиПоНоменклатуреИИнвНомеру.Субконто2
		             |ИТОГИ
		             |	СУММА(Количество),
		             |	МАКСИМУМ(СуммаОстаток),
		             |	СУММА(КоличествоОстаток),
		             |	МАКСИМУМ(КоличествоОстатокПоНоменклатуре)
		             |ПО
		             |	Номенклатура";
		
		СубконтоНоменклатура=Новый Массив(1);
		СубконтоНоменклатура[0]=ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
		
		СубконтоНомИИнвНомер=Новый Массив(2);
		СубконтоНомИИнвНомер[0]=ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
		СубконтоНомИИнвНомер[1]=ПланыВидовХарактеристик.ВидыСубконто.ИнвНомер;	
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("МоментИтогов", МоментВремени());
		Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
		ЗАпрос.УстановитьПараметр("СубконтоНоменклатура", СубконтоНоменклатура);
		Запрос.УстановитьПараметр("СубконтоНомИИнвНомер", СубконтоНомИИнвНомер);
		
		ВыборкаНоменклатура=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНоменклатура.Следующий() Цикл
			
			ВсегоКолПоНоменклатуре=ВыборкаНоменклатура.КоличествоОстатокПоНоменклатуре;
			ВсегоСумммаПоНоменклатуре=ВыборкаНоменклатура.СуммаОстаток;
			
			КоличествоОсталосьСписать=ВыборкаНоменклатура.КоличествоОстатокПоНоменклатуре;
			СуммаОсталосьСписать=ВыборкаНоменклатура.СуммаОстаток;
			
			Выборка=ВыборкаНоменклатура.Выбрать();
			Пока Выборка.Следующий() Цикл
				Превышение=Выборка.Количество - Выборка.КоличествоОстаток;
				Если Превышение>0 Тогда
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = "Превышение остатка по инвентарному номеру "+Выборка.ИнвНомерПредставление +" и номенклатуре "+ Выборка.НоменклатураПредставление+ " в количестве "+Превышение;
					Сообщение.Сообщить();
					Отказ=Истина;
				КонецЕсли;
				
				Если Отказ Тогда 
					Продолжить;
				КонецЕсли;
				
					
				Проводка = Движения.Управленческий.Добавить();
				Проводка.Период=Дата;
				Проводка.СчетДт=ПланыСчетов.Управленческий.ПрибылиУбытки;
				Проводка.СчетКт=ПланыСчетов.Управленческий.Товары;
				Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура]=Выборка.Номенклатура;
				Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ИнвНомер]=Выборка.ИнвНомер;
				Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Подразделение]=Подразделение;
				Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура]=Выборка.Номенклатура;
				Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ИнвНомер]=Выборка.ИнвНомер;
				Проводка.КоличествоКт=Выборка.Количество;
				
				Если Выборка.Количество=КоличествоОсталосьСписать Тогда
					СуммаСписываем=СуммаОсталосьСписать;
				Иначе
					СуммаСписываем=Окр (Выборка.КоличествоОстаток/ВсегоКолПоНоменклатуре*ВсегоСумммаПоНоменклатуре, 2);
				КонецЕсли;
				
				Проводка.Сумма=СуммаСписываем;
				
				Проводка = Движения.Управленческий.Добавить();
				Проводка.Период=Дата;
				Проводка.СчетДт=ПланыСчетов.Управленческий.Покупатели;
				Проводка.СчетКт=ПланыСчетов.Управленческий.ПрибылиУбытки;
				Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура]=Выборка.Номенклатура;
				Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ИнвНомер]=Выборка.ИнвНомер;
				Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Подразделение]=Подразделение;
				Проводка.Сумма=Выборка.Сумма;					
				
				КоличествоОсталосьСписать=КоличествоОсталосьСписать-Выборка.КоличествоОстаток;
				СуммаОсталосьСписать=СуммаОсталосьСписать-СуммаСписываем
				
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;      
	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу=СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры
