
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Оперативный учет
	Если ОУ Тогда
		Движения.ОстаткиТоваров.Записывать=Истина;
		Движения.ОстаткиТоваров.БлокироватьДляИзменения=Истина;
		Движения.ОстаткиТоваров.Очистить();
		
		Запрос=Новый Запрос;
		Запрос.МенеджерВременныхТаблиц=Новый МенеджерВременныхТаблиц;
		Запрос.Текст="ВЫБРАТЬ
		             |	ЕСТЬNULL(СоставСтеллажей.Деталь, РасходнаяНакладнаяСписокНоменклатуры.Номенклатура) КАК Номенклатура,
		             |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество * ЕСТЬNULL(СоставСтеллажей.Количество, 1)) КАК Количество
		             |ПОМЕСТИТЬ ТабЧасть
		             |ИЗ
		             |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставСтеллажей КАК СоставСтеллажей
		             |		ПО РасходнаяНакладнаяСписокНоменклатуры.Номенклатура = СоставСтеллажей.Стеллаж
		             |ГДЕ
		             |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		             |
		             |СГРУППИРОВАТЬ ПО
		             |	ЕСТЬNULL(СоставСтеллажей.Деталь, РасходнаяНакладнаяСписокНоменклатуры.Номенклатура)
		             |;
		             |
		             |////////////////////////////////////////////////////////////////////////////////
		             |ВЫБРАТЬ
		             |	ТабЧасть.Номенклатура,
		             |	ТабЧасть.Количество
		             |ИЗ
		             |	ТабЧасть КАК ТабЧасть";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Выборка=Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Движение=Движения.ОстаткиТоваров.ДобавитьРасход();		
			Движение.Период=Дата;
			Движение.Склад=Склад;
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
		КонецЦикла;
		
		Движения.Записать();
		
		Запрос.Текст="ВЫБРАТЬ
		|	ОстаткиТоваровОстатки.Номенклатура,
		|	-ОстаткиТоваровОстатки.КоличествоОстаток КАК Количество,
		|	ОстаткиТоваровОстатки.Номенклатура.Представление
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваров.Остатки(
		|			&МоментИтогов,
		|			Номенклатура В
		|					(ВЫБРАТЬ
		|						А.Номенклатура
		|					ИЗ
		|						ТабЧасть КАК А)
		|				И Склад = &Склад) КАК ОстаткиТоваровОстатки
		|ГДЕ
		|	ОстаткиТоваровОстатки.КоличествоОстаток < 0";
		
		Запрос.УстановитьПараметр("Склад", Склад);
		Запрос.УстановитьПараметр("МоментИтогов", Новый Граница(МоментВремени()));
		Результат=Запрос.Выполнить();
		
		Если Не Результат.Пустой() Тогда		
			Отказ=Истина;
			Выборка=Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Превышение остатка по номенклатуре "+Выборка.НоменклатураПредставление+ " в количестве "+Выборка.Количество;
				Сообщение.Сообщить();
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	
	
	
	// Бухгалтерский учет
	Если БУ Тогда
		МетодСписания=РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(МоментВремени()).МетодСписания;
		Если Не ЗначениеЗаполнено(МетодСписания) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не заполнена учетная политика";
			Сообщение.Сообщить();
			Отказ=Истина;		
			Возврат;
		КонецЕсли;
		
		Движения.Управленческий.Записывать=Истина;
		Движения.Управленческий.БлокироватьДляИзменения=Истина;
		Движения.Управленческий.Очистить();
		Движения.Управленческий.Записать();
		
		Блокировка=Новый БлокировкаДанных;
		ЭлементБлокировки=Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
		ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
		ЭлементБлокировки.ИсточникДанных=СписокНоменклатуры;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
		Блокировка.Заблокировать();
		
		Запрос=Новый Запрос;
		ТекстЗапроса="ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ТабЧасть
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТабЧасть.Номенклатура КАК Номенклатура,
		|	ТабЧасть.Количество КАК Количество,
		|	ТабЧасть.Сумма,
		|	ЕСТЬNULL(УправленческийОстаткиПоСкладу.КоличествоСкладОстаток, 0) КАК КолСкладОстаток,
		|	ЕСТЬNULL(УправленческийОстаткиПоПартии.КоличествоПартияОстаток, 0) КАК КолПартияОстаток,
		|	ЕСТЬNULL(УправленческийОстаткиПоПартии.СуммаОстаток, 0) КАК СуммаОстаток,
		|	УправленческийОстаткиПоПартии.Субконто2 КАК Партия,
		|	ТабЧасть.Номенклатура.Представление
		|ИЗ
		|	ТабЧасть КАК ТабЧасть
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
		|				&МоментИтогов,
		|				Счет = &СчетТовары,
		|				&СубконтоСклад,
		|				Субконто1 В
		|						(ВЫБРАТЬ
		|							А.Номенклатура
		|						ИЗ
		|							ТабЧасть КАК А)
		|					И Субконто2 = &Склад) КАК УправленческийОстаткиПоСкладу
		|		ПО ТабЧасть.Номенклатура = УправленческийОстаткиПоСкладу.Субконто1
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
		|				&МоментИтогов,
		|				Счет = &СчетТовары,
		|				&СубконтоПартии,
		|				Субконто1 В
		|					(ВЫБРАТЬ
		|						А.Номенклатура
		|					ИЗ
		|						ТабЧасть КАК А)) КАК УправленческийОстаткиПоПартии
		|		ПО ТабЧасть.Номенклатура = УправленческийОстаткиПоПартии.Субконто1
		|
		|УПОРЯДОЧИТЬ ПО
		|	УправленческийОстаткиПоПартии.Субконто2.МоментВремени
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(КолСкладОстаток)
		|ПО
		|	Номенклатура";
		
		Если МетодСписания=Перечисления.МетодыСписания.ЛИФО Тогда
			ТекстЗапроса=СтрЗаменить(ТекстЗапроса, "УправленческийОстаткиПоПартии.Субконто2.МоментВремени", "УправленческийОстаткиПоПартии.Субконто2.МоментВремени УБЫВ");
		КонецЕсли;
		
		Запрос.Текст=ТекстЗапроса;
		
		СубконтоПартии = Новый Массив(2);
		СубконтоПартии[0]=ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
		СубконтоПартии[1]=ПланыВидовХарактеристик.ВидыСубконто.Партия;
		
		СубконтоСклад = Новый Массив(2);
		СубконтоСклад[0]=ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
		СубконтоСклад[1]=ПланыВидовХарактеристик.ВидыСубконто.Склад;
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("МоментИтогов", МоментВремени());
		Запрос.УстановитьПараметр("Склад", Склад);
		Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
		Запрос.УстановитьПараметр("СубконтоПартии",СубконтоПартии);
		Запрос.УстановитьПараметр("СубконтоСклад",СубконтоСклад);
		
		ВыборкаНоменклатура=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНоменклатура.Следующий() Цикл
			Превышение=ВыборкаНоменклатура.Количество-ВыборкаНоменклатура.КолСкладОстаток;
			Если Превышение>0 Тогда
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Превышение остатка по номенклатуре "+ВыборкаНоменклатура.НоменклатураПредставление+" в количестве "+Превышение;
				Сообщение.Сообщить();
				Отказ=Истина;
			КонецЕсли;
			
			Если Отказ Тогда 
				Продолжить;
			КонецЕсли;
			
			ОсталосьСписать=ВыборкаНоменклатура.Количество;
			
			Выборка=ВыборкаНоменклатура.Выбрать();
			Пока Выборка.Следующий() и ОсталосьСписать<>0 Цикл
				Списываем= Мин (ОсталосьСписать, Выборка.КолПартияОстаток);
				Проводка=Движения.Управленческий.Добавить();
				Проводка.Период=Дата;
				Проводка.СчетДт=ПланыСчетов.Управленческий.ПрибылиУбытки;
				Проводка.СчетКт=Планысчетов.Управленческий.Товары;
				Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура]=Выборка.Номенклатура;
				Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Партия]=Выборка.Партия;
				Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склад]=Склад;
				Проводка.КоличествоПартияКт=Списываем;
				Проводка.КоличествоСкладКт=Списываем;
				Если Списываем=Выборка.КолПартияОстаток Тогда
					Проводка.Сумма=Выборка.СуммаОстаток;
				Иначе
					Проводка.Сумма=Списываем/Выборка.КолПартияОстаток * Выборка.СуммаОстаток;
				КонецЕсли;
				
				ОсталосьСписать=ОсталосьСписать-Списываем;
			КонецЦикла;
			
			
		КонецЦикла;
		
		Проводка=Движения.Управленческий.Добавить();
		Проводка.Период=Дата;
		Проводка.СчетДт=ПланыСчетов.Управленческий.Покупатели;
		Проводка.СчетКт=Планысчетов.Управленческий.ПрибылиУбытки;
		Проводка.Сумма=СписокНоменклатуры.Итог("Сумма");    	
	КонецЕсли;
	
	
КонецПроцедуры
