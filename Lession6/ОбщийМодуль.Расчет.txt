
Процедура РассчитатьНачисления(Регистратор) Экспорт
	
	РассчитатьОсновныеНачисления(Регистратор);
	РассчитьДопНачисления (Регистратор);
	
КонецПроцедуры

Процедура РассчитатьОсновныеНачисления(Регистратор) 
	
	НаборЗаписей=РегистрыРасчета.ОсновныеНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить (Регистратор);
	НаборЗаписей.Прочитать();
	
	//Фиксированный оклад
	Запрос = Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	ОсновныеНачисленияДанныеГрафика.НомерСтроки,
	             |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.КоличествоДнейПериодДействия, 0) КАК ДнейПлан,
	             |	ОсновныеНачисленияДанныеГрафика.Параметр
	             |ИЗ
	             |	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
	             |			Регистратор = &Регистратор
	             |				И ВидРасчета = &ВидРасчетаОклад) КАК ОсновныеНачисленияДанныеГрафика";
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ВидРасчетаОклад", ПланыВидовРасчета.ОсновныеНачисления.ФиксированныйОклад);
	
	СтруктураПоиска=Новый Структура;
	СтруктураПоиска.Вставить("НомерСтроки", 0);
	
	Выборка=Запрос.Выполнить().Выбрать();
	Для Каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки=Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат=Выборка.Параметр;
			Запись.РабочихДней=Выборка.ДнейПлан;
		КонецЕсли;
		Выборка.Сбросить();
	КонецЦикла;
	
	
	//Отпуск
	Запрос.Текст="ВЫБРАТЬ
	             |	ОсновныеНачисленияДанныеГрафика.НомерСтроки,
	             |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.КоличествоДнейФактическийПериодДействия, 0) КАК ДнейФакт,
	             |	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.РабочихДнейБаза, 0) КАК ДнейБаза,
	             |	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза, 0) + ЕСТЬNULL(ОсновныеНачисленияБазаДопНачисления.РезультатБаза, 0) КАК База
	             |ИЗ
	             |	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
	             |			Регистратор = &Регистратор
	             |				И ВидРасчета = &ВидРасчетаОтпуск) КАК ОсновныеНачисленияДанныеГрафика
	             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(
	             |				&Измерения,
	             |				&Измерения,
	             |				,
	             |				Регистратор = &Регистратор
	             |					И ВидРасчета = &ВидРасчетаОтпуск) КАК ОсновныеНачисленияБазаОсновныеНачисления
	             |		ПО ОсновныеНачисленияДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки
	             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаДопНачисления(
	             |				&Измерения,
	             |				&Измерения,
	             |				,
	             |				Регистратор = &Регистратор
	             |					И ВидРасчета = &ВидРасчетаОтпуск) КАК ОсновныеНачисленияБазаДопНачисления
	             |		ПО ОсновныеНачисленияДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаДопНачисления.НомерСтроки";
	
	Измерения = Новый Массив(1);
	Измерения[0]="Сотрудник";
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ВидРасчетаОтпуск", ПланыВидовРасчета.ОсновныеНачисления.Отпуск);
	
	
	Выборка=Запрос.Выполнить().Выбрать();
	Для Каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки=Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат=?(Выборка.ДнейБаза=0, 0, Выборка.База/Выборка.ДнейБаза * Выборка.ДнейФакт);
		КонецЕсли;
		Выборка.Сбросить();
	КонецЦикла;
	
	НаборЗаписей.Записать(,Истина);	
	
	
КонецПроцедуры


Процедура РассчитьДопНачисления(Регистратор) 
	
	НаборЗаписей=РегистрыРасчета.ДопНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить (Регистратор);
	НаборЗаписей.Прочитать();
	ПериодРегистрации=НачалоМесяца (Регистратор.Дата);
	
	Запрос = Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	ДопНачисления.НомерСтроки,
	|	ДопНачисления.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВтДопНачисления
	|ИЗ
	|	РегистрРасчета.ДопНачисления КАК ДопНачисления
	|ГДЕ
	|	ДопНачисления.Регистратор = &Регистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтДопНачисления.НомерСтроки,
	|	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.ПроцентНадбавки, 0) КАК Процент,
	|	ЕСТЬNULL(УправленческийОборотыДтКт.СуммаОборот, 0) КАК Сумма
	|ИЗ
	|	ВтДопНачисления КАК ВтДопНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
	|				&ДатаСреза,
	|				Сотрудник В
	|					(ВЫБРАТЬ
	|						А.Сотрудник
	|					ИЗ
	|						ВтДопНачисления КАК А)) КАК СведенияОСотрудникахСрезПоследних
	|		ПО ВтДопНачисления.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.ОборотыДтКт(
	|				&НачалоПериода,
	|				&КонецПериода,
	|				,
	|				СчетДт = &СчетКасса,
	|				&Субконто,
	|				СчетКт = &СчетПокупатели,
	|				,
	|				СубконтоДт1 В
	|					(ВЫБРАТЬ
	|						А.Сотрудник
	|					ИЗ
	|						ВтДопНачисления КАК А)) КАК УправленческийОборотыДтКт
	|		ПО ВтДопНачисления.Сотрудник = УправленческийОборотыДтКт.СубконтоДт1";
	
	Субконто = Новый Массив(1);
	Субконто[0]=ПланыВидовХарактеристик.ВидыСубконто.Сотрудник;
	
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("НачалоПериода", ДобавитьМесяц(ПериодРегистрации,-1));
	Запрос.УстановитьПараметр("КонецПериода", ПериодРегистрации-1);
	Запрос.УстановитьПараметр("ДатаСреза", ПериодРегистрации);
	Запрос.УстановитьПараметр("СчетКасса", ПланыСчетов.Управленческий.Касса);
	Запрос.УстановитьПараметр("СчетПокупатели", ПланыСчетов.Управленческий.Покупатели);
	Запрос.УстановитьПараметр("Субконто", Субконто);
	
	
	СтруктураПоиска=Новый Структура;
	СтруктураПоиска.Вставить("НомерСтроки", 0);
	
	Выборка=Запрос.Выполнить().Выбрать();
	Для Каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки=Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат=Выборка.Сумма * Выборка.Процент / 100;
			Запись.Параметр=Выборка.Процент;
		КонецЕсли;
		Выборка.Сбросить();
	КонецЦикла;
	
	НаборЗаписей.Записать(,Истина);	
	
	
КонецПроцедуры