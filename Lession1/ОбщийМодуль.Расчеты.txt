
Процедура Рассчитать(Ссылка, Дата, Движения) Экспорт
	
	//РАСЧЕТ ОКЛАДА
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия, 0) КАК ДнейПлан,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия, 0) КАК ДнейФакт
		|ПОМЕСТИТЬ ДанныеГрафика
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Ссылка
		|				И ВидРасчета = &Оклад) КАК ОсновныеНачисленияДанныеГрафика
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеГрафика.НомерСтроки,
		|	ДанныеГрафика.Сотрудник,
		|	ДанныеГрафика.ДнейПлан,
		|	ДанныеГрафика.ДнейФакт,
		|	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0) КАК Оклад
		|ИЗ
		|	ДанныеГрафика КАК ДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
		|				&ПериодРегистрации,
		|				Сотрудник В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						ДанныеГрафика.Сотрудник
		|					ИЗ
		|						ДанныеГрафика КАК ДанныеГрафика)) КАК СведенияОСотрудникахСрезПоследних
		|		ПО ДанныеГрафика.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник";
	
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("ПериодРегистрации", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Для каждого ТекСтрока Из Движения.ОсновныеНачисления Цикл
		Выборка.Сбросить();
		Выборка.НайтиСледующий(ТекСтрока.НомерСтроки, "НомерСтроки");
		ТекСтрока.Оклад = Выборка.Оклад;
		ТекСтрока.Результат = ?(Выборка.ДнейПлан = 0, 0, Выборка.Оклад / Выборка.ДнейПлан * Выборка.ДнейФакт); 
	КонецЦикла; 
	Движения.ОсновныеНачисления.Записать(, Истина);
	
	
	/// РАСЧЕТ ПРЕМИИ
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки,
		|	ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ДополнительныеНачисленияБазаОсновныеНачисления.РезультатБаза, 0) КАК Оклад,
		|	ЕСТЬNULL(ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник.НачальныеСтаж + РАЗНОСТЬДАТ(ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник.ДатаПриема, ДополнительныеНачисленияБазаОсновныеНачисления.БазовыйПериодНачало, ДЕНЬ) / 365, 0) КАК ТрудовойСтаж
		|ПОМЕСТИТЬ ДанныеСотрудника
		|ИЗ
		|	РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(
		|			&Измерения,
		|			&Измерения,
		|			,
		|			Регистратор = &Ссылка
		|				И ВидРасчета = &Премия) КАК ДополнительныеНачисленияБазаОсновныеНачисления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеСотрудника.НомерСтроки,
		|	ДанныеСотрудника.Оклад,
		|	ДанныеСотрудника.ТрудовойСтаж,
		|	ЕСТЬNULL(ПроцентыПремии.ПроцентПремии, 0) КАК ПроцентПремии
		|ИЗ
		|	ДанныеСотрудника КАК ДанныеСотрудника
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцентыПремии КАК ПроцентыПремии
		|		ПО ДанныеСотрудника.ТрудовойСтаж >= ПроцентыПремии.ОтработаноОт
		|			И ДанныеСотрудника.ТрудовойСтаж < ПроцентыПремии.ОтработаноДо";
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Премия", ПланыВидовРасчета.ДополнительныеНачисления.Премия);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Для каждого ТекСтрока Из Движения.ДополнительныеНачисления  Цикл
		Выборка.Сбросить();
		Выборка.НайтиСледующий(ТекСтрока.НомерСтроки, "НомерСтроки");
		ТекСтрока.Стаж = Выборка.ТрудовойСтаж;
		ТекСтрока.Процент = Выборка.ПроцентПремии;
		ТекСтрока.Результат = Выборка.Оклад * Выборка.ПроцентПремии / 100; 
	КонецЦикла; 
	Движения.ДополнительныеНачисления.Записать(, Истина);    	
КонецПроцедуры
