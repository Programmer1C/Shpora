
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОстаткиТоваров.Записывать=Истина;
	Движения.ОстаткиТоваров.БлокироватьДляИзменения=Истина;
	Движения.ОстаткиТоваров.Очистить();
	Движения.ОстаткиТоваров.Записать();
	
	Блокировка=Новый БлокировкаДанных;
	ЭлементБлокировки=Блокировка.Добавить("РегистрНакопления.ОстаткиТоваров");
	ЭлементБлокировки.ИсточникДанных=СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	             |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
	             |ПОМЕСТИТЬ ТабЧасть
	             |ИЗ
	             |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	             |ГДЕ
	             |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	             |	И НЕ РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.Услуга
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
	             |
	             |ИНДЕКСИРОВАТЬ ПО
	             |	Номенклатура
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	ТабЧасть.Номенклатура,
	             |	ТабЧасть.Количество,
	             |	ОстаткиТоваровОстатки.Склад,
	             |	ЕСТЬNULL(ОстаткиТоваровОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	             |	ЕСТЬNULL(ОстаткиТоваровОстатки.СуммаОстаток, 0) КАК СуммаОстаток
	             |ПОМЕСТИТЬ ТабЧастьСОстатками
	             |ИЗ
	             |	ТабЧасть КАК ТабЧасть
	             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваров.Остатки(
	             |				&МоментИтогов,
	             |				Номенклатура В
	             |					(ВЫБРАТЬ
	             |						Т.Номенклатура
	             |					ИЗ
	             |						ТабЧасть КАК Т)) КАК ОстаткиТоваровОстатки
	             |		ПО ТабЧасть.Номенклатура = ОстаткиТоваровОстатки.Номенклатура
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	ТабЧастьСОстатками.Номенклатура КАК Номенклатура,
	             |	ТабЧастьСОстатками.Количество КАК Количество,
	             |	ТабЧастьСОстатками.Склад,
	             |	ТабЧастьСОстатками.КоличествоОстаток КАК КоличествоОстаток,
	             |	ВЫБОР
	             |		КОГДА ТабЧастьСОстатками.Склад = &Склад
	             |			ТОГДА -1
	             |		ИНАЧЕ ЕСТЬNULL(ПриоритетыСкладовСрезПоследних.Приоритет, 99999)
	             |	КОНЕЦ КАК Приоритет,
	             |	ТабЧастьСОстатками.Номенклатура.Представление,
	             |	ТабЧастьСОстатками.СуммаОстаток
	             |ИЗ
	             |	ТабЧастьСОстатками КАК ТабЧастьСОстатками
	             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПриоритетыСкладов.СрезПоследних(
	             |				&МоментИтогов,
	             |				Склад В
	             |					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	             |						А.Склад
	             |					ИЗ
	             |						ТабЧастьСОстатками КАК А)) КАК ПриоритетыСкладовСрезПоследних
	             |		ПО ТабЧастьСОстатками.Склад = ПриоритетыСкладовСрезПоследних.Склад
	             |
	             |УПОРЯДОЧИТЬ ПО
	             |	Приоритет
	             |ИТОГИ
	             |	МАКСИМУМ(Количество),
	             |	СУММА(КоличествоОстаток)
	             |ПО
	             |	Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментИтогов", МоментВремени());
	Запрос.УстановитьПараметр("Склад", Склад);
	
	ВыборкаНоменклатура = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Превышение = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
		Если Превышение>0 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Превышение остатка по номенклатуре "+ВыборкаНоменклатура.НоменклатураПредставление+" в количестве "+Превышение;
			Сообщение.Сообщить();
			Отказ=Истина;
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		ОсталосьСписать = ВыборкаНоменклатура.Количество;
		Выборка=ВыборкаНоменклатура.Выбрать();
		Пока Выборка.Следующий() и ОсталосьСписать<>0 Цикл
			Списываем=Мин (ОсталосьСписать, Выборка.КоличествоОстаток);
			Движение=Движения.ОстаткиТоваров.ДобавитьРасход();
			Движение.Период=Дата;
			Движение.Номенклатура=Выборка.Номенклатура;
			Движение.Склад=Выборка.Склад;
			Движение.Количество=Списываем;
			Если Списываем=Выборка.КоличествоОстаток Тогда
				Движение.Сумма=Выборка.СуммаОстаток;
			Иначе
				Движение.Сумма=Списываем/Выборка.КоличествоОстаток * Выборка.СуммаОстаток;
			КонецЕсли;
			
			ОсталосьСписать=ОсталосьСписать-Списываем;
		КонецЦикла;
	КонецЦикла;
	
	
КонецПроцедуры
