Процедура РассчитатьНачисления (Регистратор) Экспорт
	
	НаборЗаписей = РегистрыРасчета.ОсновныеНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить (Регистратор);
	НаборЗаписей.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	ОсновныеНачисленияДанныеГрафика.Параметр,
	             |	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.КоличествоЧасовФактическийПериодДействия, 0) КАК ЧасовФакт,
	             |	ОсновныеНачисленияДанныеГрафика.НомерСтроки
	             |ИЗ
	             |	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(Регистратор = &Регистратор) КАК ОсновныеНачисленияДанныеГрафика";
				 
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	Выборка=Запрос.Выполнить().Выбрать();
	СтруктураПоиска=Новый Структура;
	СтруктураПоиска.Вставить("НомерСтроки", 0);
	Для Каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки=Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат = Выборка.ЧасовФакт * Выборка.Параметр;
		КонецЕсли;
		Выборка.Сбросить();
	КонецЦикла;
	
	НаборЗаписей.Записать(, Истина);
	
	
	ДопНаборЗаписей = РегистрыРасчета.ДопНачисления.СоздатьНаборЗаписей();
	ДопНаборЗаписей.Отбор.Регистратор.Установить (Регистратор);
	ДопНаборЗаписей.Прочитать();
	
	Запрос.Текст="ВЫБРАТЬ
	             |	ДопНачисленияБазаДопНачисления.НомерСтроки,
	             |	СУММА(ДопНачисленияБазаДопНачисления.РезультатБаза) КАК База
	             |ИЗ
	             |	РегистрРасчета.ДопНачисления.БазаДопНачисления(
	             |			&Измерения,
	             |			&Измерения,
	             |			&Разрезы,
	             |			Регистратор = &Регистратор
	             |				И ВидРасчета = &ВидРасчетаПремия) КАК ДопНачисленияБазаДопНачисления
	             |ГДЕ
	             |	ДопНачисленияБазаДопНачисления.Сотрудник <> ДопНачисленияБазаДопНачисления.СотрудникРазрез
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	ДопНачисленияБазаДопНачисления.НомерСтроки";
				 
				 Измерения = Новый Массив(1);
				 Измерения[0]="Подразделение";
				 
				 Разрезы=Новый Массив(1);
				 Разрезы[0]="Сотрудник";
				 
				 Запрос.УстановитьПараметр("ВидРасчетаПремия", ПланыВидовРасчета.ДополнительныеНачисления.Премия);
				 Запрос.УстановитьПараметр("ИЗмерения", Измерения);
				 Запрос.УстановитьПараметр("Разрезы",  Разрезы);
	
	Выборка=Запрос.Выполнить().Выбрать();
	Для Каждого Запись Из ДопНаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки=Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат = Выборка.База * Запись.Параметр/100;
		КонецЕсли;
		Выборка.Сбросить();
	КонецЦикла;
	
	ДопНаборЗаписей.Записать(, Истина);

	
	
КонецПроцедуры