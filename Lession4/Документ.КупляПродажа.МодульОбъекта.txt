
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Управленческий.Записывать=Истина;
	Движения.Управленческий.БлокироватьДляИзменения=Истина;
	Движения.Управленческий.Очистить();
	Движения.Управленческий.Записать();
	
	Блокировка=Новый БлокировкаДанных;
	ЭлементБлокировки=Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
	ЭлементБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склад, СкладПоставщика);
	ЭлементБлокировки.УстановитьЗначение("Организация", ОрганизацияПоставщик);
	ЭлементБлокировки.ИсточникДанных=СписокТоваров;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
	Блокировка.Заблокировать();
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	КупляПродажаСписокТоваров.Номенклатура КАК Номенклатура,
	             |	СУММА(КупляПродажаСписокТоваров.Количество) КАК Количество,
	             |	СУММА(КупляПродажаСписокТоваров.Сумма) КАК Сумма
	             |ПОМЕСТИТЬ ТабЧасть
	             |ИЗ
	             |	Документ.КупляПродажа.СписокТоваров КАК КупляПродажаСписокТоваров
	             |ГДЕ
	             |	КупляПродажаСписокТоваров.Ссылка = &Ссылка
	             |
	             |СГРУППИРОВАТЬ ПО
	             |	КупляПродажаСписокТоваров.Номенклатура
	             |
	             |ИНДЕКСИРОВАТЬ ПО
	             |	Номенклатура
	             |;
	             |
	             |////////////////////////////////////////////////////////////////////////////////
	             |ВЫБРАТЬ
	             |	ТабЧасть.Номенклатура КАК Номенклатура,
	             |	ТабЧасть.Количество КАК Количество,
	             |	ТабЧасть.Сумма КАК Сумма,
	             |	УправленческийОстатки.Субконто2 КАК Партия,
	             |	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	             |	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
	             |	ТабЧасть.Номенклатура.Представление
	             |ИЗ
	             |	ТабЧасть КАК ТабЧасть
	             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
	             |				&МоментИтогов,
	             |				Счет = &СчетТовары,
	             |				&Субконто,
	             |				Субконто1 В
	             |						(ВЫБРАТЬ
	             |							А.Номенклатура
	             |						ИЗ
	             |							Табчасть КАК А)
	             |					И Организация = &ОрганизацияПоставщик
	             |					И Субконто3 = &СкладПоставщика) КАК УправленческийОстатки
	             |		ПО ТабЧасть.Номенклатура = УправленческийОстатки.Субконто1
	             |ИТОГИ
	             |	МАКСИМУМ(Количество),
	             |	МАКСИМУМ(Сумма),
	             |	СУММА(КоличествоОстаток)
	             |ПО
	             |	Номенклатура";
	
	Субконто=Новый Массив (3);
	Субконто[0]=ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	Субконто[1]=ПланыВидовХарактеристик.ВидыСубконто.Партия;
	Субконто[2]=ПланыВидовХарактеристик.ВидыСубконто.Склад;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментИтогов", МоментВремени());
	Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
	Запрос.УстановитьПараметр("Субконто", Субконто);
	Запрос.УстановитьПараметр("СкладПоставщика", СкладПоставщика);
	Запрос.УстановитьПараметр("ОрганизацияПоставщик", ОрганизацияПоставщик);
	
	ВыборкаНоменклатура = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Превышение=ВыборкаНоменклатура.Количество-ВыборкаНоменклатура.КоличествоОстаток;
		Если Превышение>0 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Превышение остатка по номенклатуре "+ВыборкаНоменклатура.НоменклатураПредставление+" в количестве "+Превышение;
			Сообщение.Сообщить();
			Отказ=Истина;
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		Выборка = ВыборкаНоменклатура.Выбрать();
		ОсталосьСписать=ВыборкаНоменклатура.Количество;
		Пока Выборка.Следующий() и ОсталосьСписать<>0 Цикл
			Списываем=Мин (ОсталосьСписать, Выборка.КоличествоОстаток);
			Проводка = Движения.Управленческий.Добавить();
			Проводка.Период=Дата;
			Проводка.Организация=ОрганизацияПоставщик;
			Проводка.СчетДт=ПланыСчетов.Управленческий.ПрибылиУбытки;
			Проводка.СчетКт=ПланыСчетов.Управленческий.Товары;
			Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура]=Выборка.Номенклатура;
			Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Партия]=Выборка.Партия;
			Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склад]=СкладПоставщика;
			Проводка.КоличествоКт=Списываем;
			Если ОсталосьСписать=Выборка.КоличествоОстаток Тогда
				Проводка.Сумма=Выборка.СуммаОстаток;
			Иначе
				Проводка.Сумма=Списываем/Выборка.КоличествоОстаток * Выборка.СуммаОстаток;
			КонецЕсли;
			ОсталосьСписать=ОсталосьСписать-Списываем;
		КонецЦикла;
		
		Проводка = Движения.Управленческий.Добавить();
		Проводка.Период=Дата;
		Проводка.Организация=ОрганизацияПокупатель;
		Проводка.СчетДт=ПланыСчетов.Управленческий.Товары;
		Проводка.СчетКт=ПланыСчетов.Управленческий.Поставщики;
		Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура]=ВыборкаНоменклатура.Номенклатура;
		Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Партия]=Ссылка;
		Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склад]=СкладПокупателя;
		Проводка.КоличествоДт=ВыборкаНоменклатура.Количество;
		Проводка.Сумма=ВыборкаНоменклатура.Сумма;
	КонецЦикла;
	
	Проводка = Движения.Управленческий.Добавить();
	Проводка.Период=Дата;
	Проводка.Организация=ОрганизацияПоставщик;	
	Проводка.СчетДт=ПланыСчетов.Управленческий.Покупатели;
	Проводка.СчетКт=ПланыСчетов.Управленческий.ПрибылиУбытки;
	Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Покупатель]=ОрганизацияПокупатель;
	Проводка.Сумма=СписокТоваров.Итог("Сумма");	    
		
			
	
КонецПроцедуры
