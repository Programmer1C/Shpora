
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОсновныеНачисления.Записывать=Истина;
	Движения.ОсновныеНачисления.Очистить();
	
	Движения.ДопНачисления.Записывать=Истина;
	Движения.ДопНачисления.Очистить();
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	НачислениеЗарплатыОсновныеНачисления.Сотрудник,
	|	НачислениеЗарплатыОсновныеНачисления.Подразделение,
	|	НачислениеЗарплатыОсновныеНачисления.ВидРасчета,
	|	НачислениеЗарплатыОсновныеНачисления.График,
	|	НачислениеЗарплатыОсновныеНачисления.ДатаНачала КАК ПериодДействияНачало,
	|	НачислениеЗарплатыОсновныеНачисления.ДатаОкончания КАК ПериодДействияКонец,
	|	НАЧАЛОПЕРИОДА(НачислениеЗарплатыОсновныеНачисления.Ссылка.Дата, МЕСЯЦ) КАК ПериодРегистрации,
	|	НачислениеЗарплатыОсновныеНачисления.Параметр
	|ИЗ
	|	Документ.НачислениеЗарплаты.ОсновныеНачисления КАК НачислениеЗарплатыОсновныеНачисления
	|ГДЕ
	|	НачислениеЗарплатыОсновныеНачисления.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеЗарплатыДопНачисления.Сотрудник,
	|	НачислениеЗарплатыДопНачисления.Подразделение,
	|	НачислениеЗарплатыДопНачисления.ВидРасчета,
	|	НАЧАЛОПЕРИОДА(НачислениеЗарплатыДопНачисления.Ссылка.Дата, МЕСЯЦ) КАК ПериодРегистрации,
	|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(НачислениеЗарплатыДопНачисления.Ссылка.Дата, МЕСЯЦ, -1), МЕСЯЦ) КАК БазовыйПериодНачало,
	|	КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(НачислениеЗарплатыДопНачисления.Ссылка.Дата, МЕСЯЦ, -1), МЕСЯЦ) КАК БазовыйПериодКонец,
	|	НачислениеЗарплатыДопНачисления.Параметр,
	|	NULL КАК Результат
	|ИЗ
	|	Документ.НачислениеЗарплаты.ДопНачисления КАК НачислениеЗарплатыДопНачисления
	|ГДЕ
	|	НачислениеЗарплатыДопНачисления.ВидРасчета = &ВидРасчетаПремия
	|	И НачислениеЗарплатыДопНачисления.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НачислениеЗарплатыДопНачисления.Сотрудник,
	|	НачислениеЗарплатыДопНачисления.Подразделение,
	|	НачислениеЗарплатыДопНачисления.ВидРасчета,
	|	НАЧАЛОПЕРИОДА(НачислениеЗарплатыДопНачисления.Ссылка.Дата, МЕСЯЦ),
	|	NULL,
	|	NULL,
	|	NULL,
	|	НачислениеЗарплатыДопНачисления.Параметр
	|ИЗ
	|	Документ.НачислениеЗарплаты.ДопНачисления КАК НачислениеЗарплатыДопНачисления
	|ГДЕ
	|	НачислениеЗарплатыДопНачисления.ВидРасчета = &ВидРасчетаНадбавка
	|	И НачислениеЗарплатыДопНачисления.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидРасчетаПремия", ПланыВидовРасчета.ДополнительныеНачисления.Премия);
	Запрос.УстановитьПараметр("ВидРасчетаНадбавка", ПланыВидовРасчета.ДополнительныеНачисления.Надбавка);
	
	Результаты = Запрос.ВыполнитьПакет();
	Выборка=Результаты[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение=Движения.ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
	Выборка=Результаты[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение=Движения.ДопНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
	Движения.Записать();
	
	Расчет.РассчитатьНачисления(Ссылка);
	
КонецПроцедуры
